#!/usr/bin/perl

# ShannonE.pl v0.1.0
# Author: MH Seabolt
# Last updated: 8-18-2021

# SYNOPSIS
# Calculates Shannon's E (Equitability) from each row (or cumulatively) from an input data matrix.
# Requires that the data be numeric and representative of frequencies/counts.


##################################################################################
# The MIT License
#
# Copyright (c) 2021 Matthew H. Seabolt
#
# Permission is hereby granted, free of charge, 
# to any person obtaining a copy of this software and 
# associated documentation files (the "Software"), to 
# deal in the Software without restriction, including 
# without limitation the rights to use, copy, modify, 
# merge, publish, distribute, sublicense, and/or sell 
# copies of the Software, and to permit persons to whom 
# the Software is furnished to do so, 
# subject to the following conditions:
#
# The above copyright notice and this permission notice 
# shall be included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, 
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES 
# OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. 
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR 
# ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, 
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE 
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
##################################################################################

use strict;
use warnings;
use Getopt::Long qw(GetOptions);

# GetOpts Variable declarations
my $input = "--";
my $output = "--";
my $cns;
my $rns;
my $sep;
my $method;
my $algorithm;

sub usage {
	my $usage = "ShannonE.pl v0.1.0\n
	PURPOSE: Calculates Shannon's E (Equitability) from each row (or cumulatively) from an input data matrix.
			Requires that the data be numeric and representative of frequencies/counts.
	\n
	USAGE:	ShannonE.pl v0.1.0 -i matrix.tab -o entropy.tab
	-i		input matrix file in TAB format.
	-o 		output file name ( do not include extensions! )
	-c 		INT flag; matrix contains column headers? ( Default: ON )
	-r 		INT flag; matrix contains row names? ( Default: ON )
	-s 		STR; input data separator. ( Default: tab )
	-m 		STR; method to calculate E (either row [Default] or cumulative).
	-a 		STR; algorithm to user for computing E (either shannon [Default] or simpson).
\n";
	print $usage;
}

GetOptions(	'input|i=s' => \$input, 
			'out|o=s' => \$output,
			'colnames|c=i' => \$cns,
			'rownames|r=i' => \$rns,
			'sep|s=s' => \$sep,
			'method|m=s' => \$method,
			'algorithm|a=s' => \$algorithm,
) or die usage();

# Parameter setups
$cns = ( $cns && $cns == 0 )? 0 : 1;
$rns = ( $rns && $rns == 0 )? 0 : 1;
$sep = ( $sep && $sep ne "\t" )? $sep : "\t";
$method = ( $method && $method =~ /cum/ )? "cumulative" : "row";
$algorithm = ( $algorithm && $algorithm eq "simpson" )? "simpson" : "shannon";

##################################################################################
# Read the input data file, expecting a tabular matrix with headers and columns
my $fh = *STDIN;
my $succin = open(MATRIX, "<", "$input") if ( $input ne "--" && -e $input );
$fh = *MATRIX if ( $succin ); 

# Open the output filehand
my $succout = open( OUT, ">", "$output" ) if $output ne "--";
my $fhout;
if ( $succout )		{	$fhout = *OUT;		}
else				{	$fhout = *STDOUT;	}

# Operate on the matrix line-by-line
my @cumulative;
while ( <$fh> )		{
	chomp $_;
	
	# If we have column headers, just print our singple header
	if ( $cns == 1 && $. == 1 )	{
		print $fhout "Equitability\n";
		next;
	}
	
	# Now operate on the rows
	my @row = split("$sep", $_);
	my $rowname = shift @row if ( $rns == 1 );
	
	if ( $method ne "cumulative" )	{
		if ( $rns == 1 )	{	print $fhout "$rowname", "$sep", sprintf("%3.4f", equitability( @row )), "\n";	}
		else 				{	print $fhout sprintf("%3.4f", equitability( @row )), "\n";	}
	}
	else 	{
		push @cumulative, $_ foreach (@row);
	}
}
close $fh if ( $succin );

# Print the cumulative Entropy if this was requested
if ( $method eq "cumulative" )	{
	if ( $rns == 1 )	{	print $fhout "Cumulative", "$sep", sprintf("%3.4f", equitability( @cumulative )), "\n";	}
	else 				{	print $fhout sprintf("%3.4f", equitability( @cumulative )), "\n";	}
}
close $fhout if ( $succout );
exit;


#################################### SUBROUTINES ##################################

# Wrapper routine for triaging the correct statistic
sub equitability	{
	my ( @sample ) = @_;
	( $algorithm eq "shannon" )? return shannonE(@sample) : return simpsonE(@sample);
}


# Calculates Shannon Entropy H'
sub shannonE		{
	my ( @sample ) = @_;
	
	# Error handling: return 0 values if $N is zero
	my $N = sum( @sample );
	return ( 0 ) if ( $N == 0 );		
	
	# Calculate *p* for each species *i* <-- this can be thought of as relative abundance, but this is only one way to estimate that measure
	my @pis;
	for ( my $i=0; $i < scalar @sample; $i++ )	{
		$pis[$i] = ( $sample[$i] == 0 )? 0 : ($sample[$i]/$N) * log($sample[$i]/$N);
	}

	# Sum @pis and multiply by -1
	my $H = -( sum(@pis) );
	my $E = ( scalar @sample <= 1 )? "Inf" : ($H / log(scalar @sample));
	return $E;
}

# Calculates Simpson's D, 1-D (Index of Diversity), 1/D (Gini-Simpson or Reciprocal Simpson), and Equitability E.
sub simpsonE		{
	my ( @sample ) = @_;
	
	# Error handling: return NA values if $N is zero
	my $N = sum( @sample );
	return ( "NA" ) if ( $N == 0 );										

	# Calculate *n* for each species *i*, times *n_{i}* - 1.
	my @nis;
	for ( my $i=0; $i < scalar @sample; $i++ )	{
		$nis[$i] = $sample[$i] * ($sample[$i] - 1);
	}

	# Calculate *D* = sum(nis) / N(N-1)
	my $D = ( sum(@nis) / ($N*($N-1)) );
	
	# Calculate evenness as 1/S * 1/D
	my $E = ( scalar @sample == 0 || $D == 0 )? "Inf" : (1/scalar @sample) * (1/$D);
	
	return $E;
}

sub sum		{
	my ( @list ) = @_;
	my $sum = 0;
	$sum += $_ foreach ( @list );
	return $sum;
}
